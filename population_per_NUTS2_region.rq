#+ summary: "Retrieve the total population for a specific NUTS region code (e.g., PL, PL41, LT, NO, or * for all)"
#+ description: "NOTE: make sure response content type is application/json"
#+ endpoint: "https://www.foodie-cloud.org/sparql"
#+ tags: [ "json" ]
#+ method: "GET"
#+ defaults:
#+   - nutsCode: DE3
#+ endpoint_in_url: False
#+ transform: {
#+   "nuts2": "?nuts2",
#+   "$anchor": "nuts2",
#+   "value": "?population",
#+ }


PREFIX  qb:   <http://purl.org/linked-data/cube#>
PREFIX  sdmx-measure: <http://purl.org/linked-data/sdmx/2009/measure#>
PREFIX  e:    <http://ontologycentral.com/2009/01/eurostat/ns#>
PREFIX  g:    <http://eurostat.linked-statistics.org/ontologies/geographic.rdf#>
PREFIX  skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX  rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX  dataset: <http://eurostat.linked-statistics.org/data/>
PREFIX nuts: <http://nuts.geovocab.org/>
PREFIX spatial: <http://geovocab.org/spatial#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX sdmx-dimension: <http://purl.org/linked-data/sdmx/2009/dimension#>
PREFIX property: <http://eurostat.linked-statistics.org/property#>
PREFIX ramon: <http://rdfdata.eionet.europa.eu/ramon/ontology/>
 
SELECT ?nuts2 ?date (SUM(?pop) AS ?population)
FROM dataset:demo_r_pjanaggr3.rdf
FROM nuts:
WHERE
{ ?observation  qb:dataSet        dataset:demo_r_pjanaggr3 ;
               property:age                     <http://eurostat.linked-statistics.org/dic/age#TOTAL> ;
               property:sex                     <http://eurostat.linked-statistics.org/dic/sex#F> ;
               property:geo                     ?ugeo ;
               #sdmx-dimension:timePeriod "2007-01-01"^^xsd:dateTime ;
               sdmx-dimension:timePeriod ?date ;
               sdmx-measure:obsValue     ?pop .
  BIND(IRI(CONCAT ("http://nuts.geovocab.org/id/",STRAFTER(STR(?ugeo),"http://eurostat.linked-statistics.org/dic/geo#"))) as ?code1) .
  ?code1        spatial:PP            ?parent .
  ?parent      rdfs:label            ?nuts2 .  
  BIND (STRAFTER(STR(?parent),"http://nuts.geovocab.org/id/") as ?code)
  FILTER (?_nutsCode = "*" || ?code = ?_nutsCode)    
  optional {?_nutsCode ?p ?o}
  #BIND (STRAFTER(STR(?period),"http://eurostat.linked-statistics.org/dic/TIME_PERIOD#") as ?year)  
  BIND (STRDT(CONCAT(?_year,"-01-01"),xsd:dateTime) AS ?date1)
  FILTER (?_year = "*" || ?date = ?date1)    
  optional {?_year ?p1 ?o1}
}
GROUP BY ?nuts2, ?date
ORDER BY ?nuts2, ?date



