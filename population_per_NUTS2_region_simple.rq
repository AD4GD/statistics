#+ summary: "Retrieve the total population for a specific NUTS region code (e.g., PL, PL41, LT, NO, or * for all) and/or for specific year <= 2023  (or * for all)"
#+ description: "NOTE: make sure response content type is application/json"
#+ endpoint: "https://www.foodie-cloud.org/sparql"
#+ tags: [ "json" ]
#+ method: "GET"
#+ defaults:
#+   - nutsCode: "DE3"
#+   - year: "2016"
#+ endpoint_in_url: False
#+ transform: {
#+   "year": "?year",
#+   "$anchor": "year",
#+   "nuts2": "?nutsParent",
#+   "value": "?population",
#+ }


PREFIX  qb:   <http://purl.org/linked-data/cube#>
PREFIX  sdmx-measure: <http://purl.org/linked-data/sdmx/2009/measure#>
PREFIX  e:    <http://ontologycentral.com/2009/01/eurostat/ns#>
PREFIX  g:    <http://eurostat.linked-statistics.org/ontologies/geographic.rdf#>
PREFIX  skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX  rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX  dataset: <http://eurostat.linked-statistics.org/data/>
PREFIX nuts: <http://nuts.geovocab.org/>
PREFIX nuts2024: <http://data.europa.eu/nuts>
PREFIX spatial: <http://geovocab.org/spatial#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX sdmx-dimension: <http://purl.org/linked-data/sdmx/2009/dimension#>
PREFIX property: <http://eurostat.linked-statistics.org/property#>
PREFIX ramon: <http://rdfdata.eionet.europa.eu/ramon/ontology/>
 

SELECT ?year ?nuts3 ?nutsParent (SUM(?pop) AS ?population)
FROM dataset:estat_demo_r_pjanaggr3.ttl
FROM nuts2024:
WHERE
  { ?observation  qb:dataSet        dataset:estat_demo_r_pjanaggr3 ;
               property:age                     <http://eurostat.linked-statistics.org/dic/age#TOTAL> ;
               property:sex                     <http://eurostat.linked-statistics.org/dic/sex#F> ;
               property:geo                     ?ugeo ;
               property:TIME_PERIOD ?period ;
               sdmx-measure:obsValue     ?pop .
  
  BIND (STRAFTER(STR(?ugeo),"http://eurostat.linked-statistics.org/dic/geo#") as ?nuts1)
  BIND(IRI(CONCAT ("http://data.europa.eu/nuts/code/",?nuts1)) as ?code1) .
  BIND (IRI(IF (STRLEN(str(?nuts1))>2, SUBSTR(str(?code1),1,STRLEN(str(?code1))-1),"")) as ?parent)
  BIND (STRAFTER(STR(?parent),"http://data.europa.eu/nuts/code/") as ?nutsParent)
  #?parent      skos:prefLabel            ?nuts2 . 
  ?parent      rdfs:label            ?nuts2 . 
  BIND(CONCAT (?nutsParent," - ",str(?nuts2)) as ?nuts3) .
        
  FILTER (?_nutsCode = "*" || ?nutsParent = ?_nutsCode)    
  optional {?_nutsCode ?p ?o}
  
  BIND (STRAFTER(STR(?period),"http://eurostat.linked-statistics.org/dic/TIME_PERIOD#") as ?year)  
  FILTER (?_year = "*" || ?year = ?_year)    
  optional {?_year ?p1 ?o1}
  }
GROUP BY ?nuts3 ?nutsParent ?year 
ORDER BY ?nuts3 ?nutsParent ?year 
  



